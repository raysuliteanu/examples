plugins {
    id 'java'
    id 'com.google.protobuf' version '0.8.13'
    id 'application'
}

group 'org.kidoni.examples'
version '0.0.1-SNAPSHOT'

repositories {
    mavenCentral()
}

ext {
    grpcVersion = '1.34.1'
    protobufVersion = '3.14.0'
    protocVersion = protobufVersion
}

dependencies {
    implementation "io.grpc:grpc-protobuf:${grpcVersion}"
    implementation "io.grpc:grpc-stub:${grpcVersion}"
    compileOnly "org.apache.tomcat:annotations-api:6.0.53"

    runtimeOnly "io.grpc:grpc-netty-shaded:${grpcVersion}"

    testImplementation "io.grpc:grpc-testing:${grpcVersion}"
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.6.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
}

protobuf {
    protoc { artifact = "com.google.protobuf:protoc:${protocVersion}" }
    plugins {
        grpc { artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}" }
    }
    generateProtoTasks {
        all()*.plugins { grpc {} }
    }
}

sourceSets {
    main {
        java {
            srcDirs 'build/generated/source/proto/main/grpc'
            srcDirs 'build/generated/source/proto/main/java'
        }
    }
}

test {
    useJUnitPlatform()
}

// application plugin-related tasks
task GreetingServer(type: CreateStartScripts) {
    mainClassName = 'org.kidoni.examples.grpc.GreetingServiceServer'
    applicationName = 'greeting-server'
    outputDir = new File(project.buildDir, 'tmp')
    classpath = startScripts.classpath
}

task GreetingClient(type: CreateStartScripts) {
    mainClassName = 'org.kidoni.examples.grpc.GreetingClient'
    applicationName = 'greeting-client'
    outputDir = new File(project.buildDir, 'tmp')
    classpath = startScripts.classpath
}

applicationDistribution.into('bin') {
    from(GreetingServer)
    from(GreetingClient)
    fileMode = 0755
}
